#version 450

#include "../global_bindings.glsl"
#include "../utilities.glsl"
#include "hybrid_fluid.glsl"
#include "particles.glsl"

layout(set = 2, binding = 0) uniform texture3D VelocityVolumeX;
layout(set = 2, binding = 1) uniform texture3D VelocityVolumeY;
layout(set = 2, binding = 2) uniform texture3D VelocityVolumeZ;
layout(set = 2, binding = 3, r8_snorm) uniform restrict image3D MarkerVolume;
layout(set = 2, binding = 4, r32ui) uniform restrict coherent uimage3D LinkedListDualGrid;
layout(set = 2, binding = 5) buffer restrict ParticlePositionLlBuffer { ParticlePositionLl Particles[]; };
layout(set = 2, binding = 6) buffer restrict ParticleBufferVx { vec4 ParticleBufferVelocityX[]; };
layout(set = 2, binding = 7) buffer restrict ParticleBufferVy { vec4 ParticleBufferVelocityY[]; };
layout(set = 2, binding = 8) buffer restrict ParticleBufferVz { vec4 ParticleBufferVelocityZ[]; };

COMPUTE_PASS_PARTICLES

vec3 InterpolateTrilinear(vec3 v_000, vec3 v_100, vec3 v_010, vec3 v_110, vec3 v_001, vec3 v_101, vec3 v_011, vec3 v_111, vec3 interpolantsX,
                          vec3 interpolantsY, vec3 interpolantsZ) {
    return mix(mix(mix(v_000, v_100, interpolantsX), mix(v_010, v_110, interpolantsX), interpolantsY),
               mix(mix(v_001, v_101, interpolantsX), mix(v_011, v_111, interpolantsX), interpolantsY), interpolantsZ);
}

float sampleVelocity(ivec3 coord, uint component) {
    if (component == 0)
        return texelFetch(VelocityVolumeX, coord, 0).x;
    else if (component == 1)
        return texelFetch(VelocityVolumeY, coord, 0).x;
    else
        return texelFetch(VelocityVolumeZ, coord, 0).x;
}

void main() {
    uint particleIndex = gl_GlobalInvocationID.x;
    if (particleIndex >= NumParticles)
        return;

    vec3 gridSize = Rendering.FluidGridResolution;
    vec3 gridTexelSizeNormalized = 1.0 / gridSize;

    vec3 originalPosition = Particles[particleIndex].Position;

    // Walls are moving, so we need to correct position right away if this particle got "eaten"
    {
        vec3 texcoord = originalPosition * gridTexelSizeNormalized;
        vec4 currentSolid = texture(sampler3D(SceneVoxelization, SamplerPointClamp), texcoord);
        if (currentSolid.w > 0.0) {
            // Find "escape cell"
            vec3 absSolidVelocity = abs(currentSolid.xyz);
            if (absSolidVelocity.x > absSolidVelocity.y) {
                if (absSolidVelocity.x > absSolidVelocity.z)
                    originalPosition.x += sign(currentSolid.x);
                else
                    originalPosition.z += sign(currentSolid.z);
            } else {
                if (absSolidVelocity.y > absSolidVelocity.z)
                    originalPosition.y += sign(currentSolid.y);
                else
                    originalPosition.z += sign(currentSolid.z);
            }
            // imageStore(DebugVolume, ivec3(originalPosition), vec4(1.0));
        }
    }

    // Sample all 8 neighboring velocity grid cells
    //
    // Notes from experimentation with texture sampling:
    // * texelFetch is either slower or as fast. Previously allowed particles outside the volume in which case it was better since we got boundaries
    // for free wiht SamplerPointClamp
    // * using linear sampler repeatedly (6x linear sample instead of 8x direct sample + manual interpolate) is indeed slower
    // (as one would hope for sanity's sake :))
    vec3 offsetPositions[] = {
        max(vec3(0.0), originalPosition - vec3(1.0, 0.5, 0.5)),
        max(vec3(0.0), originalPosition - vec3(0.5, 1.0, 0.5)),
        max(vec3(0.0), originalPosition - vec3(0.5, 0.5, 1.0)),
    };

    vec3 v_000, v_100, v_010, v_110, v_001, v_101, v_011, v_111;
    [[unroll]] for (int i = 0; i < 3; ++i) {
        ivec3 volumeCoordMin = ivec3(offsetPositions[i]);
        ivec3 volumeCoordMax = min(volumeCoordMin + ivec3(1, 1, 1), ivec3(gridSize - ivec3(1)));

        v_000[i] = sampleVelocity(volumeCoordMin, i);
        v_100[i] = sampleVelocity(ivec3(volumeCoordMax.x, volumeCoordMin.yz), i);
        v_010[i] = sampleVelocity(ivec3(volumeCoordMin.x, volumeCoordMax.y, volumeCoordMin.z), i);
        v_110[i] = sampleVelocity(ivec3(volumeCoordMax.xy, volumeCoordMin.z), i);
        v_001[i] = sampleVelocity(ivec3(volumeCoordMin.xy, volumeCoordMax.z), i);
        v_101[i] = sampleVelocity(ivec3(volumeCoordMax.x, volumeCoordMin.y, volumeCoordMax.z), i);
        v_011[i] = sampleVelocity(ivec3(volumeCoordMin.x, volumeCoordMax.yz), i);
        v_111[i] = sampleVelocity(volumeCoordMax, i);
    }

    // Update velocity and the APIC affine matrix.
    // Trilinear interpolation for velocity.
    vec3 interpolantsX = fract(vec3(offsetPositions[0].x, offsetPositions[1].x, offsetPositions[2].x));
    vec3 interpolantsY = fract(vec3(offsetPositions[0].y, offsetPositions[1].y, offsetPositions[2].y));
    vec3 interpolantsZ = fract(vec3(offsetPositions[0].z, offsetPositions[1].z, offsetPositions[2].z));
    vec3 v_x00 = mix(v_000, v_100, interpolantsX);
    vec3 v_x01 = mix(v_001, v_101, interpolantsX);
    vec3 v_x10 = mix(v_010, v_110, interpolantsX);
    vec3 v_x11 = mix(v_011, v_111, interpolantsX);
    vec3 v_xy0 = mix(v_x00, v_x10, interpolantsY);
    vec3 v_xy1 = mix(v_x01, v_x11, interpolantsY);
    vec3 newVelocity = mix(v_xy0, v_xy1, interpolantsZ);

    // The Apic Matrix is the jacobi matrix of the velocity for fluids.
    vec3 cx = mix(mix(v_100, v_110, interpolantsY), mix(v_101, v_111, interpolantsY), interpolantsZ) -
              mix(mix(v_000, v_010, interpolantsY), mix(v_001, v_011, interpolantsY), interpolantsZ);
    vec3 cy = mix(v_x10, v_x11, interpolantsZ) - mix(v_x00, v_x01, interpolantsZ);
    vec3 cz = v_xy1 - v_xy0;

    // Advect with Runge Kutta 4
    // confined to current cell for which we know velocities are defined and we already sampled all corners - which makes this relatively cheap.
    vec3 k1 = newVelocity;
    vec3 stepK2 = Time.SimulationDelta * 0.5 * k1;
    vec3 k2 = InterpolateTrilinear(v_000, v_100, v_010, v_110, v_001, v_101, v_011, v_111, saturate(interpolantsX + stepK2),
                                   saturate(interpolantsY + stepK2), saturate(interpolantsZ + stepK2));
    vec3 stepK3 = Time.SimulationDelta * 0.5 * k2;
    vec3 k3 = InterpolateTrilinear(v_000, v_100, v_010, v_110, v_001, v_101, v_011, v_111, saturate(interpolantsX + stepK3),
                                   saturate(interpolantsY + stepK3), saturate(interpolantsZ + stepK3));
    vec3 stepK4 = Time.SimulationDelta * k3;
    vec3 k4 = InterpolateTrilinear(v_000, v_100, v_010, v_110, v_001, v_101, v_011, v_111, saturate(interpolantsX + stepK4),
                                   saturate(interpolantsY + stepK4), saturate(interpolantsZ + stepK4));
    vec3 totalMovement = Time.SimulationDelta * (1.0 / 6.0) * (k1 + 2.0 * (k2 + k3) + k4);
    vec3 newPosition = originalPosition + totalMovement;

    // Prevent entering solid cells.
    // We would need to conservatively step along the movement line and check for any cell on the way if we are allowed to enter
    // This is quite expensive (and complicated)! Instead we assume that particles are usually slow, so we just check the "target cell"
    // If we're not allowed to be there, we set the particle to the border of its current cell (with some margin)
    // (yes, truncating the movement vector like this doesn't do the RK4 we just did any justice)
    {
        vec3 texcoord = newPosition * gridTexelSizeNormalized;
        // Wall penetration.
        if (clamp(newPosition, vec3(1.001), vec3(Rendering.FluidGridResolution - 1.001)) != newPosition ||
            texture(sampler3D(SceneVoxelization, SamplerPointClamp), texcoord).w > 0.0) {
            // the length can't be 0, otherwise we wouldn't enter here. But result may be inaccurate.
            float currentStepLength = length(totalMovement) + 1e-10;
            vec3 movementDirection = totalMovement / currentStepLength;
            vec3 absMovementDirection = abs(movementDirection);

            vec3 posInCell = fract(originalPosition);
            float maxAllowedStep = currentStepLength;
            maxAllowedStep = min(maxAllowedStep, (movementDirection.x > 0.0 ? posInCell.x : 1.0 - posInCell.x) / absMovementDirection.x - 0.001);
            maxAllowedStep = min(maxAllowedStep, (movementDirection.y > 0.0 ? posInCell.y : 1.0 - posInCell.y) / absMovementDirection.y - 0.001);
            maxAllowedStep = min(maxAllowedStep, (movementDirection.z > 0.0 ? posInCell.z : 1.0 - posInCell.z) / absMovementDirection.z - 0.001);

            // Correct position and velocity so we really do not penetrate that cell.
            totalMovement = movementDirection * maxAllowedStep;

            // If we were in this cell to begin with we are in the rare case of "stuck anyways".
            if (ivec3(originalPosition) == ivec3(newPosition)) {
                // Note, we're using smooth interpolation here to estimate a gradient.
                vec3 pushForce =
                    vec3(texture(sampler3D(SceneVoxelization, SamplerTrilinearClamp), texcoord - vec3(gridTexelSizeNormalized.x, 0.0, 0.0)).w -
                             texture(sampler3D(SceneVoxelization, SamplerTrilinearClamp), texcoord + vec3(gridTexelSizeNormalized.x, 0.0, 0.0)).w,
                         texture(sampler3D(SceneVoxelization, SamplerTrilinearClamp), texcoord - vec3(0.0, gridTexelSizeNormalized.y, 0.0)).w -
                             texture(sampler3D(SceneVoxelization, SamplerTrilinearClamp), texcoord + vec3(0.0, gridTexelSizeNormalized.y, 0.0)).w,
                         texture(sampler3D(SceneVoxelization, SamplerTrilinearClamp), texcoord - vec3(0.0, 0.0, gridTexelSizeNormalized.z)).w -
                             texture(sampler3D(SceneVoxelization, SamplerTrilinearClamp), texcoord + vec3(0.0, 0.0, gridTexelSizeNormalized.z)).w);
                totalMovement += pushForce * (Time.SimulationDelta * 50.0);
            }

            newPosition = originalPosition + totalMovement;
            newPosition = clamp(newPosition, vec3(1.001), vec3(Rendering.FluidGridResolution - 1.001));

            newVelocity = (movementDirection * maxAllowedStep) / Time.SimulationDelta;

            // Do we also need to change the derivatives of velocity? Maybe just scale them?
        }
    }

    // Write new LinkedList & marker grid for density projection step.
    {
        // TODO: Consider not changing, density_projection_gather_error step can do this easily by checking the linkedlist volume
        imageStore(MarkerVolume, ivec3(newPosition), vec4(CELL_FLUID, 0, 0, 0));
        ivec3 nearestDualGridCell = ivec3(newPosition - vec3(0.5));
        Particles[particleIndex].LinkedListNext = imageAtomicExchange(LinkedListDualGrid, nearestDualGridCell, particleIndex + 1) - 1;
    }

    // Write out particle.
    {
        Particles[particleIndex].Position = newPosition;
        ParticleBufferVelocityX[particleIndex] = vec4(cx, newVelocity.x);
        ParticleBufferVelocityY[particleIndex] = vec4(cy, newVelocity.y);
        ParticleBufferVelocityZ[particleIndex] = vec4(cz, newVelocity.z);
        // PIC only
        // ParticleBufferVelocityX[particleIndex] = vec4(vec3(0), newVelocity.x);
        // ParticleBufferVelocityY[particleIndex] = vec4(vec3(0), newVelocity.y);
        // ParticleBufferVelocityZ[particleIndex] = vec4(vec3(0), newVelocity.z);
    }
}